plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'exchange.dydx.cartera'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

//    configurations.all {
//        resolutionStrategy.eachDependency { details ->
//            if (details.requested.group == 'org.bouncycastle'
//                    && !details.requested.name.contains('bcprov-jdk15on') ) {
//                details.useVersion "1.70"
//            }
//        }
//    }

    configurations {
        all {
            exclude group: 'org.bouncycastle', module: 'bcprov-jdk15to18'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
   // implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    implementation 'com.google.code.gson:gson:2.9.0'

    //
    // https://github.com/WalletConnect/WalletConnectKotlinV2
    //

    implementation platform('com.walletconnect:android-bom:1.15.0')
    implementation('com.walletconnect:android-core')
    implementation 'com.walletconnect:sign'
    implementation 'com.walletconnect:push'

    //
    // https://docs.cloud.coinbase.com/wallet-sdk/docs/android-install
    //
    implementation 'com.coinbase:coinbase-wallet-sdk:1.0.4'

    //
    // https://github.com/WalletConnect/kotlin-walletconnect-lib
    //
    implementation 'com.github.WalletConnect:kotlin-walletconnect-lib:0.9.9'
}

apply plugin: 'maven-publish'
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "dydxprotocol"
            artifactId "cartera-android"
            version "0.0.6"
            artifact sourceJar
            artifact "$buildDir/outputs/aar/cartera-release.aar"

            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        if (it.group != null || it.name != null || it.version != null || it.name == "unspecified") return

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            def github_username = System.getenv('github_username')
            def github_token = System.getenv('github_token')
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dydxprotocol/cartera-android")
            credentials {
                username github_username
                password github_token
            }
        }
    }
}